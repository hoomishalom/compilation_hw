/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import ast.*;
import java.util.*;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
        java_cup.runtime.Symbol s = (java_cup.runtime.Symbol) info;
		throw new RuntimeException("ERROR(" + (s.left + 1) + ")"); // left + 1 is the line number
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getTokenStartPosition());
	System.out.print("] ");
	System.out.print(s.sym);
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal TYPE_INT;
terminal TYPE_STRING;
terminal TYPE_VOID;
terminal LT;
terminal GT;
terminal NIL;
terminal NEW;
terminal CLASS;
terminal EXTENDS;
terminal ARRAY;
terminal COMMA;
terminal RETURN;
terminal ELSE;

/*************/
/* TERMINALS */
/*************/
terminal String STRING;
terminal Integer INT;
terminal String ID;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AstExp exp;
non terminal AstVar var;
non terminal AstStmt stmt;
non terminal AstStmtList stmtList;
non terminal AstDecl dec;
non terminal AstProgram program;
non terminal AstType type;
non terminal AstVarDecl varDec;
non terminal ArrayList<AstFormal> funcArgs;
non terminal AstFuncDecl funcDec;
non terminal AstClassDecl classDec;
non terminal AstArrayDecl arrayTypedef;
non terminal ArrayList<AstFormal> funcArgList;
non terminal AstStmtList elseOpt;
non terminal AstFormal funcArg;
non terminal ArrayList<AstDecl> cFieldList;
non terminal AstDecl cField;
non terminal ArrayList<AstExp> comExpList;
non terminal ArrayList<AstExp> comExpListOpt;
non terminal AstExp newExp;
non terminal AstCall callExp;
non terminal String optionalId;
non terminal AstExp assExpOpt;

non terminal ArrayList<AstDecl> declList;         																						
non terminal AstExp varDecExp;					 											
non terminal ArrayList<AstFormal> funcArgOpt;														
non terminal Boolean negOpt	;						
non terminal AstExp literal;													 																					
non terminal AstStmtIf ifDef;
non terminal AstStmtWhile whileDef;		
		


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN; 
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/************************/
/* START WITH: stmtList */
/************************/
start with program;

/********************/
/* DERIVATION RULES */
/********************/
					
program ::= declList:l {: RESULT = new AstProgram(l, lleft + 1); :};

declList ::= declList:l dec:d {: l.add(d); RESULT = l; :}
           | dec:d {: ArrayList<AstDecl> l = new ArrayList<AstDecl>(); l.add(d); RESULT = l; :};

dec 		::=		varDec:v          {: RESULT = v; :}
					| funcDec:f       {: RESULT = f; :}
					| classDec:c      {: RESULT = c; :}
					| arrayTypedef:a  {: RESULT = a; :}
					;

type		::=		TYPE_INT:t		{: RESULT = new AstType("int", tleft + 1); 		:} // TODO maybe split to dedicated classes
					| TYPE_VOID:t		{: RESULT = new AstType("void", tleft + 1); 		:}
					| TYPE_STRING:t	{: RESULT = new AstType("string", tleft + 1); 	:}
					| ID:name		{: RESULT = new AstType(name, nameleft + 1); :}
					;

varDecExp 	::=		exp:e {: RESULT = e; :}
					| newExp:e {: RESULT = e; :}
					;
assExpOpt 	::=		ASSIGN varDecExp:e {: RESULT = e; :}
					| {: RESULT = null; :}
					;

varDec		::= 	type:t ID:name assExpOpt:e SEMICOLON {: RESULT = new AstVarDecl(t, name, e, tleft + 1); :}
					;


funcArg		::=		type:t ID:name {: RESULT = new AstFormal(t, name, tleft + 1); :}
					;

funcArgs	::=		funcArgs:l COMMA funcArg:f {: l.add(f); RESULT = l; :}
					| funcArg:f {: ArrayList l = new ArrayList<AstFormal>(); l.add(f); RESULT = l; :}
					;

funcArgOpt	::=		funcArgs:l {: RESULT = l; :}
					| {: RESULT = new ArrayList<AstFormal>(); :}
					;

funcDec 	::= 	type:t ID:name LPAREN funcArgOpt:args RPAREN LBRACE stmtList:body RBRACE
					{: RESULT = new AstFuncDecl(t, name, args, body, tleft + 1); :}
					;

stmtList	::=		stmtList:l stmt:s		{: l.statements.add(s); RESULT = l;    :}
					| stmt:s {: AstStmtList l = new AstStmtList(sleft + 1); l.statements.add(s); RESULT = l; :}
					;

cField		::=		varDec:v {: RESULT = v; :}
					| funcDec:f {: RESULT = f; :}
					;

cFieldList	::=		cFieldList:l cField:f {: l.add(f); RESULT = l; :}
					| cField:f {: ArrayList<AstDecl> l = new ArrayList<AstDecl>(); l.add(f); RESULT = l; :}
					;

optionalId	::= 	EXTENDS ID:id {: RESULT = id; :}
					| {: RESULT = null; :}
					;

classDec	::= 	CLASS:c ID:name optionalId:superName LBRACE cFieldList:fields RBRACE
					{: RESULT = new AstClassDecl(name, superName, fields, cleft + 1); :}
					;

arrayTypedef ::=	ARRAY:a ID:name EQ type:t LBRACK RBRACK SEMICOLON
					{: RESULT = new AstArrayDecl(t, name, aleft + 1); :}
					;

literal		::= 	INT:i    {: RESULT = new AstExpInt(i, ileft + 1);     :}
                    | MINUS:m INT:i {: RESULT = new AstExpNegInt(i, mleft + 1); :}
					| STRING:s {: RESULT = new AstExpString(s, sleft + 1);  :}
					| NIL:n      {: RESULT = new AstExpNil(nleft + 1);      :}
					;
exp			::=		var:v {: RESULT = new AstExpVar(v, vleft + 1); :}
					| LPAREN exp:e RPAREN {: RESULT = e; :}
					| exp:e1 LT:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.LT, e1left + 1); :}
					| exp:e1 GT:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.GT, e1left + 1); :}
					| exp:e1 EQ:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.EQ, e1left + 1); :}
					| exp:e1 PLUS:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.PLUS, e1left + 1); :}
					| exp:e1 MINUS:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.MINUS, e1left + 1); :}
					| exp:e1 TIMES:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.TIMES, e1left + 1); :}
					| exp:e1 DIVIDE:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.DIVIDE, e1left + 1); :}
					| callExp:c {: RESULT = c; :}
					| literal:l {: RESULT = l; :}
					;

newExp 		::= 	NEW:n type:t {: RESULT = new AstNewExp(t, null, nleft + 1); :} 
					| NEW:n type:t LBRACK exp:e RBRACK {: RESULT = new AstNewExp(t, e, nleft + 1); :} 
					;


comExpList	::=		comExpList:l COMMA exp:e {: l.add(e); RESULT = l; :}
						| exp:e {: ArrayList l = new ArrayList<AstExp>(); l.add(e); RESULT = l; :}
						;

comExpListOpt ::=   comExpList:l {: RESULT = l; :}
                    | {: RESULT = new ArrayList<AstExp>(); :}
                    ;

callExp		::= 	ID:name LPAREN comExpListOpt:args RPAREN {: RESULT = new AstCall(null, name, args, nameleft + 1); :}
					| var:v DOT ID:name LPAREN comExpListOpt:args RPAREN {: RESULT = new AstCall(v, name, args, vleft + 1); :}
					;

var			::=		ID:name								{: RESULT = new AstVarSimple(name, nameleft + 1);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AstVarField(v,fieldName, vleft + 1); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AstVarSubscript(v,e, vleft + 1);     :}
					;

whileDef	::=		WHILE:w LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE
					{: RESULT = new AstStmtWhile(cond,body, wleft + 1); :}
					;
ifDef		::=		IF:i LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE elseOpt:e	{: RESULT = new AstStmtIf(   cond,body, e, ileft + 1); :}
					;
					
elseOpt		::=		ELSE LBRACE stmtList:elseBody RBRACE {: RESULT = elseBody; :}
					| {: RESULT = null; :}
					;
stmt		::=		varDec:v {: RESULT = v; :}
					| var:v ASSIGN varDecExp:e SEMICOLON	{: RESULT = new AstStmtAssign(v,e, vleft + 1); :}
					| RETURN:r exp:e SEMICOLON {: RESULT = new AstReturnStmt(e, rleft + 1); :}
					| RETURN:r SEMICOLON {: RESULT = new AstReturnStmt(null, rleft + 1); :}
					| ifDef:i {: RESULT = i; :}
					| whileDef:w {: RESULT = w; :}
					| callExp:c SEMICOLON {: RESULT = new AstCallStmt(c, cleft + 1); :}
					;
 


