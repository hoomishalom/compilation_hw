/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import ast.*;
import java.util.*;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
        java_cup.runtime.Symbol s = (java_cup.runtime.Symbol) info;
		throw new RuntimeException("ERROR(" + (s.left + 1) + ")"); // left + 1 is the line number
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getTokenStartPosition());
	System.out.print("] ");
	System.out.print(s.sym);
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal TYPE_INT;
terminal TYPE_STRING;
terminal TYPE_VOID;
terminal LT;
terminal GT;
terminal NIL;
terminal NEW;
terminal CLASS;
terminal EXTENDS;
terminal ARRAY;
terminal COMMA;
terminal RETURN;
terminal ELSE;

/*************/
/* TERMINALS */
/*************/
terminal String STRING;
terminal Integer INT;
terminal String ID;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AstExp exp;
non terminal AstVar var;
non terminal AstStmt stmt;
non terminal AstStmtList stmtList;
non terminal AstDecl dec;
non terminal AstProgram program;
non terminal AstType type;
non terminal AstVarDecl varDec;
non terminal ArrayList<AstFormal> funcArgs;
non terminal AstFuncDecl funcDec;
non terminal AstClassDecl classDec;
non terminal AstArrayDecl arrayTypedef;
non terminal ArrayList<AstFormal> funcArgList;
non terminal AstFormal funcArg;
non terminal ArrayList<AstDecl> cFieldList;
non terminal AstDecl cField;
non terminal ArrayList<AstExp> comExpList;
non terminal ArrayList<AstExp> comExpListOpt;
non terminal AstExp newExp;
non terminal AstCall callExp;
non terminal String optionalId;
non terminal AstExp assExpOpt;

non terminal ArrayList<AstDecl> declList;         																						
non terminal AstExp varDecExp;					 											
non terminal ArrayList<AstFormal> funcArgOpt;														
non terminal Boolean negOpt	;						
non terminal AstExp literal;													 																					
non terminal AstStmtIf ifDef;
non terminal AstStmtWhile whileDef;		
		


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN; 
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/************************/
/* START WITH: stmtList */
/************************/
start with program;

/********************/
/* DERIVATION RULES */
/********************/
					
program ::= declList:l {: RESULT = new AstProgram(l); :};

declList ::= declList:l dec:d {: l.add(d); RESULT = l; :}
           | {: RESULT = new ArrayList<AstDecl>(); :};

dec 		::=		varDec:v          {: RESULT = v; :}
					| funcDec:f       {: RESULT = f; :}
					| classDec:c      {: RESULT = c; :}
					| arrayTypedef:a  {: RESULT = a; :}
					;

type		::=		TYPE_INT		{: RESULT = new AstType("int"); 		:} // TODO maybe split to dedicated classes
					| TYPE_VOID		{: RESULT = new AstType("void"); 		:}
					| TYPE_STRING	{: RESULT = new AstType("string"); 	:}
					| ID:name		{: RESULT = new AstType(name); :}
					;

varDecExp 	::=		exp:e {: RESULT = e; :}
					| newExp:e {: RESULT = e; :}
					;
assExpOpt 	::=		ASSIGN varDecExp:e {: RESULT = e; :}
					| {: RESULT = null; :}
					;

varDec		::= 	type:t ID:name assExpOpt:e SEMICOLON {: RESULT = new AstVarDecl(t, name, e); :}
					;


funcArg		::=		type:t ID:name {: RESULT = new AstFormal(t, name); :}
					;

funcArgs	::=		funcArgs:l COMMA funcArg:f {: l.add(f); RESULT = l; :}
					| funcArg:f {: ArrayList l = new ArrayList<AstFormal>(); l.add(f); RESULT = l; :}
					;

funcArgOpt	::=		funcArgs:l {: RESULT = l; :}
					| {: RESULT = new ArrayList<AstFormal>(); :}
					;

funcDec 	::= 	type:t ID:name LPAREN funcArgOpt:args RPAREN LBRACE stmtList:body RBRACE
					{: RESULT = new AstFuncDecl(t, name, args, body); :}
					;

stmtList	::=		stmtList:l stmt:s		{: l.statements.add(s); RESULT = l;    :}
					| {: RESULT = new AstStmtList(); :}
					;

cField		::=		varDec:v {: RESULT = v; :}
					| funcDec:f {: RESULT = f; :}
					;

cFieldList	::=		cFieldList:l cField:f {: l.add(f); RESULT = l; :}
					| {: RESULT = new ArrayList<AstDecl>(); :}
					;

optionalId	::= 	EXTENDS ID:id {: RESULT = id; :}
					| {: RESULT = null; :}
					;

classDec	::= 	CLASS ID:name optionalId:superName LBRACE cFieldList:fields RBRACE
					{: RESULT = new AstClassDecl(name, superName, fields); :}
					;

arrayTypedef ::=	ARRAY ID:name EQ type:t LBRACK RBRACK SEMICOLON
					{: RESULT = new AstArrayDecl(t, name); :}
					;

literal		::= 	INT:i    {: RESULT = new AstExpInt(i);     :}
                    | MINUS INT:i {: RESULT = new AstExpNegInt(i); :}
					| STRING:s {: RESULT = new AstExpString(s);  :}
					| NIL      {: RESULT = new AstExpNil();      :}
					;
exp			::=		var:v {: RESULT = new AstExpVar(v); :}
					| LPAREN exp:e RPAREN {: RESULT = e; :}
					| exp:e1 LT:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.LT); :}
					| exp:e1 GT:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.GT); :}
					| exp:e1 EQ:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.EQ); :}
					| exp:e1 PLUS:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.PLUS); :}
					| exp:e1 MINUS:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.MINUS); :}
					| exp:e1 TIMES:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.TIMES); :}
					| exp:e1 DIVIDE:op exp:e2 {: RESULT = new AstExpBinop(e1, e2, AstExpBinop.OP.DIVIDE); :}
					| callExp:c {: RESULT = c; :}
					| literal:l {: RESULT = l; :}
					;

newExp 		::= 	NEW type:t {: RESULT = new AstNewExp(t, null); :} 
					| NEW type:t LBRACK exp:e RBRACK {: RESULT = new AstNewExp(t, e); :} 
					;


comExpList	::=		comExpList:l COMMA exp:e {: l.add(e); RESULT = l; :}
						| exp:e {: ArrayList l = new ArrayList<AstExp>(); l.add(e); RESULT = l; :}
						;

comExpListOpt ::=   comExpList:l {: RESULT = l; :}
                    | {: RESULT = new ArrayList<AstExp>(); :}
                    ;

callExp		::= 	ID:name LPAREN comExpListOpt:args RPAREN {: RESULT = new AstCall(null, name, args); :}
					| var:v DOT ID:name LPAREN comExpListOpt:args RPAREN {: RESULT = new AstCall(v, name, args); :}
					;

var			::=		ID:name								{: RESULT = new AstVarSimple(name);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AstVarField(v,fieldName); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AstVarSubscript(v,e);     :}
					;

whileDef	::=		WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE
					{: RESULT = new AstStmtWhile(cond,body); :}
					;
ifDef		::=		IF LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AstStmtIf(   cond,body, null); :}
					| IF LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE ELSE LBRACE stmtList:elseBody RBRACE	{: RESULT = new AstStmtIf(   cond,body, elseBody); :}
					;					
stmt		::=		varDec:v {: RESULT = v; :}
					| var:v ASSIGN varDecExp:e SEMICOLON	{: RESULT = new AstStmtAssign(v,e); :}
					| RETURN exp:e SEMICOLON {: RESULT = new AstReturnStmt(e); :}
					| RETURN SEMICOLON {: RESULT = new AstReturnStmt(null); :}
					| ifDef:i {: RESULT = i; :}
					| whileDef:w {: RESULT = w; :}
					| callExp:c SEMICOLON {: RESULT = new AstCallStmt(c); :}
					;
 


